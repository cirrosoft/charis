buildscript {
    ext {
        springBootVersion = '1.5.7.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "org.flywaydb.flyway" version "5.0.7"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

group = 'com.cirrosoft.charis'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

bootRun {
    systemProperties = System.properties
}

flyway {
    url = 'jdbc:mysql://localhost:3306/db'
    user = 'root'
    password = 'password'
}

task stopDbKill(type: Exec) { commandLine 'docker', 'kill', "db" }
task stopDb(type: Exec) { commandLine 'docker', 'rm', '-f', "db" }
task startDb(type: Exec) {
    commandLine 'docker',
            'run',
            '-p', "3306:3306",
            "--name", "db",
            "-e", "MYSQL_ROOT_PASSWORD=password",
            "-e", "MYSQL_DATABASE=db",
            "-d", "mysql:5.7"
}
stopDb.dependsOn(stopDbKill)

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.boot:spring-boot-starter-mustache")
    compile("mysql:mysql-connector-java:6.0.5")
}
